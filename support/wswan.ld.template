OUTPUT_FORMAT("elf32-i386")
ENTRY(_start)
MEMORY
{
	ROM (rx) : ORIGIN = %ROM_AREA_START%, LENGTH = %ROM_AREA_LENGTH%
	RAM (wx) : ORIGIN = %RAM_HEAP_START%, LENGTH = %RAM_HEAP_LENGTH%
}
SECTIONS
{
	/* segelf segment start markers for target text section. */
	".text!" %ROM_AREA_START% (NOLOAD) :
	{
		"__stext!" = .;
		KEEP(*(".start!"))
		*(".text!*" ".text.*!")
		"__etext!" = .;
	} >ROM

	/* Target text section. */
	.text . :
	{
		__stext = .;
		KEEP(*(".start"))
		*(.text ".text.*[^&]")
		__etext = .;
		. = ALIGN (16);
	}

	"__rom_bank_offset!" = 0;
	"__rom_bank_offset" = ABSOLUTE(%ROM_BANK_OFFSET%);

	/* segelf segment end markers for target text section. */
	".text&" . (NOLOAD) :
	{
		"__stext&" = .;
		KEEP(*(".start&"))
		*(".text&*" ".text.*&")
		"__etext&" = .;
	}

	.fartext ALIGN (0x10) : SUBALIGN (0x10) {
		*(SORT (".fartext!*"))
		*(SORT (".fartext$*"))
		*(SORT (".fartext&*"))
		*(SORT (".fartext.*"))
		. = .;
	}

	.farrodata ALIGN (0x10) : SUBALIGN (0x10) {
		*(SORT (".farrodata!*"))
		*(SORT (".farrodata$*"))
		*(SORT (".farrodata&*"))
		*(SORT (".farrodata.*"))
		. = .;
	}

	.erom . (NOLOAD) :
	{
		. = ALIGN (16);
		"__erom" = .;
		"__erom!" = .;
		"__erom&" = .;
		. = .;
	}

	/* segelf segment start markers for target data section. */
	".data!" %RAM_HEAP_START% (NOLOAD) : AT(ADDR(".erom") + SIZEOF(".erom"))
	{
		"__sdata!" = .;
		*(".ivt!")
		*(".rodata!*" ".rodata.*!")
		*(".data!*" ".data.*!")
		*(".bss!*" ".bss.*!")
		"__edata!" = .;
		"__ebss!" = .;
	} >RAM

	/* IVT header - we don't want to store it in ROM. */
	.ivt %RAM_HEAP_START% (NOLOAD) :
	{
		KEEP(*(.ivt))
	}

	/* Target data section. */
	__sdata = .;
	".data" . : AT(ADDR(".erom") + SIZEOF(".erom"))
	{
		*(.rodata ".rodata.*[^&]")
		*(.data ".data.*[^&]")
	}
	__edata = .;
	"__ldata!" = 0;
	__ldata = SIZEOF(.data);
	"__ldata_words!" = 0;
	__ldata_words = (__ldata + 1) / 2;

	__sbss = .;
	/* Target BSS section. */
	.bss . (NOLOAD) :
	{
		*(.bss ".bss.*[^&]")
	}
	__ebss = .;
	"__lbss!" = 0;
	__lbss = SIZEOF(.bss);
	"__lbss_words!" = 0;
	__lbss_words = (__lbss + 1) / 2;

	/* segelf segment end markers for target data section. */
	".data&" . (NOLOAD) :
	{
		"__sdata&" = .;
		*(".ivt&")
		*(".rodata&*" ".rodata.*&")
		*(".data&*" ".data.*&")
		*(".bss&*" ".bss.*&")
		"__edata&" = .;
		"__ebss& " = .;
	}

	"__eheap!" = 0;
	__eheap = %RAM_HEAP_LENGTH%;
}
